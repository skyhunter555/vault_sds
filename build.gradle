plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.google.protobuf' version '0.8.8'
    id 'application'
}

mainClassName = 'ru.vault.sds.VaultSdsMain'

group = 'ru.vault'
version = '1.0.0'
description = 'Приложение для тестирования vault'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

sourceSets["main"].java.srcDirs("src/main/java", "gen/main/java")

repositories {
    mavenCentral()
}

dependencies {


    implementation 'com.google.protobuf:protobuf-java:3.7.1'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'

    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.2'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'org.springframework.vault:spring-vault-core:2.2.3.RELEASE'
    implementation 'org.apache.httpcomponents:httpclient:4.5.8'

    implementation 'net.devh:grpc-spring-boot-starter:2.13.1.RELEASE'
    implementation 'io.grpc:grpc-netty:1.30.0'
    implementation 'io.grpc:grpc-protobuf:1.30.0'
    implementation 'io.grpc:grpc-stub:1.30.0'
    implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.0.0'
    implementation group: 'io.envoyproxy.controlplane', name: 'server', version: '0.1.31'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.0.3.RELEASE'
    testImplementation group: 'junit', name: 'junit', version: '4.12'

}

configurations {
    all.collect { configuration ->
        configuration.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

protobuf {

    generatedFilesBaseDir = "$projectDir/gen"

    protoc {
        artifact = "com.google.protobuf:protoc:3.7.1"
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.20.0'
        }
        reactor {
            artifact = 'com.salesforce.servicelibs:reactor-grpc:1.0.0'
        }
    }

    generateProtoTasks {
        ofSourceSet("main").forEach { task ->
            task.plugins {
                // Apply the "grpc" plugin whose spec is defined above, without options.
                grpc {
                    outputSubDir = "java"
                }
                reactor {
                    outputSubDir = 'java'
                }
            }
        }
    }
}
